/*
	共通機能
*/


// # ----------------------------------------------------------------
// # メール


// メールチェック開始が指示
// R2 [M]チェックするメールサーバ名[CS]チェックするアカウント名
OnBIFFBegin
{
	if ! ISVAR('TmpBIFFCheckAc'){TmpGVList,='TmpBIFFCheckAc'}
	TmpBIFFCheckAc = R[2]

	"\0\s[0]\i[31]%(username)さんのメールボックス、\w6見てみますね。\w6\i[10]\w2\s[5]\nマスカレード。\w9\1\s[12]むう。\e"
	'\0\s[0]\i[31]じゃ、\w6メールのチェックを。\w6\i[10]\nマスカレード。\w9\1\s[11]……\w6俺？\e'
}

// チェック成功
// R0 スプールメール数(1origin)　R1 スプールメールバイト数　R2 チェックしたメールサーバ名
// R3 新着差分　R4 全メールのTopResult　R5 ListResult　R6 UidlResult
OnBIFFComplete
{
	// R0 スプールメール数(1origin)　R1 スプールメールバイト数
	// R2 チェックしたメールサーバ名　R3 新着差分　R4 全メールのTopResult
	// R5 ListResult　R6 UidlResult

	if ! R[3]
	{
		// 無かった
		'\1\s[10]新着、\w6無し。\w6\0\s[8]……\w6そ、\w6そう？\e'
		'\1\s[11]新着無かった。\w6\0\s[6]\i[31]……\w6確認ありがとっ。\e'
	}
	else
	{
		// 届いていた
		"\1\s[10]あった。\w6\s[11]\n%(R[3])通の新着メール発見。\w9\0"
		--
		"\s[5]%(username)さんのお友達の人からかな？\e"
		'\s[0]\i[34]おめでとうございますっ。\e'
		'\s[5]\i[34]おめでとうございますっ。\w9\1\s[11]\n\n……\w6めでたいか？\e'
	}
}



// メールチェック失敗
// R0 失敗理由(timeout-タイムアウト/kick-パスワード認証失敗/defect-ユーザー設定に誤り)
// R2 [M]チェックしたメールサーバ名[CS]チェックしたアカウント名
OnBIFFFailure
{
	if R[0]=='timeout'
	{
		'\1\s[16]タイムアウト。\w6\s[11]\n激重とか、\w6オフラインとか。\w6\0\s[8]確認してみて下さいっ。\e'
	}
	elseif R[0]=='kick'
	{
		'\1\s[15]蹴られた。\w6\0\s[2]だ、\w6大丈夫っ？\w9\n怪我は？\w9\1\s[11]\n\n……\w6ないです。\e'
		"\1\s[15]蹴られた。\w6\s[11]\n\nどうした%(username2)。\w6\0\s[0]？\e"
		'\1\s[11]失敗。\w9\0\s[2]げ、\w6原因はっ？\w9\1\s[14]\n\n俺には破れない結界が張ってある。\w9\s[12]\nくそう、\w6俺の封印が解けさえすればこんな結界\0\s[6]\i[71]\i[34]\i[60]\n\n原因はパスワードあたりだそうですっ。\e'
	}
	elseif R[0]=='defect'
	{
		// 設定誤り
		'\1\s[11]……\w6なんか、\w6設定間違ってる感じがするな。\w6\0\s[0]\i[31]見直してみてください……。\e'
	}
}


// メールスプール数が増加した
// R0 スプールメール数(1origin)　R1 スプールメールバイト数
// R2 [M]チェックメールサーバ名[CS]チェックアカウント名
// R3全メールのTopResult
//OnBIFF2Complete{}

























// # ----------------------------------------------------------------
// # ヘッドラインセンス


// [MS]ヘッドラインセンス開始が指示された
// R0 HLSするサイト名　R1 ヘッドラインセンスするサイトのURL
OnHeadlinesenseBegin
{
	'\0\s[0]\i[31]拾ってきたラジオで情報収集してみます。\w6\i[10]\n\n何か動きはあるでしょうか……？\w9\e'
	'\0\s[1]拾ってきたラジオ、\w6つけてみますね。\w9\1\s[11]動くのか？\w9\e'
}


// [MS]読み上げるべきヘッドラインセンスがあった
// R0 センスするサイト名　R1 センスするサイトのURL
// R2 OnFindフェーズ(First-1ページ目/First and Last-1ページ目で次がない/Last-最後のページ/Next- いずれでもないページ)
// R3 ヘッドラインの内容
OnHeadlinesense.OnFind
{
	if R[2] == 'First' || R[2] == 'First and Last'
	{
		// 最初のページ
		"\0\b[2]\s[1]\_q『%(R[0])』ラジオ、ヘッドライン。"
	}
	elseif R[2] == 'Next'
	{
		// 真ん中あたりのページ
		"\0\b[2]\s[1]\_q『%(R[0])』ラジオ、ヘッドライン続き。"
	}
	elseif R[2] == 'Last'
	{
		// 最後のページ
		"\0\b[2]\s[0]\i[31]\_q『%(R[0])』ラジオ、ヘッドライン終わり。"
	}
	--
	"\_q\n\n%(R[3])\n\n"
	--
	if R[2] == 'First' || R[2] == 'Next'
	{
		// 次のページがある場合
		"\q0[][次ページ]/
		\q1[%(R[1])][サイトへ]/
		\q2[HeadLineCancel][閉じる]\e"
	}
	else
	{
		// 次のページがない場合
		"\q0[%(R[1])][サイトへ]/
		\q1[HeadLineCancel][閉じる]\e"
	}
}

// [MS]ヘッドラインセンスが正常に終了
// R0 終了理由(no update-更新がなかった/can't download-ファイルが取れなかった/can't analyze-ファイルは取れたが有効なヘッドラインを取得できなかった)
OnHeadlinesenseComplete
{
	'\0\s[8]……\w6特にニュースはないみたいですっ。\e'
	'\0\s[1]……\w6特になにもないみたいです。\w6\1\s[11]まあ平和ですこと。\e'
}

// [MS]ヘッドラインセンス失敗
// R0 終了理由(no update-更新がなかった/can't download-ファイルが取れなかった/can't analyze-ファイルは取れたが有効なヘッドラインを取得できなかった)
OnHeadlinesenseFailure
{
	if reference0 == "can%(CHR(39))t download"
	{
		'\1\s[11]電波が悪いな。\w6\s[13]\nこれじゃ受信できない。\w6\0\s[3]う、\w6うーん……。\e'
	}
	elseif reference0 == "can%(CHR(39))t analyze"
	{
		'\1\s[11]……\w6入るには入るんだが、\w6\nよく聞こえんな。\w6\0\s[0]？\e'
	}
	else
	{
		'\1\s[11]……\w6なんか、\w6ダメだ。\w6\s[14]\nラジオ壊れてないか？\w6\0\s[0]\i[32]\i[71]\i[60]え……。\e'
		'\1\s[11]……\w6なんかおかしいな。\w6\n調子が悪い。\w6\0\s[8]ひ、\w6拾ったものですし……。\e'
	}
}























// # ----------------------------------------------------------------
// # 時計あわせ


// 時計合わせが開始が指示された際
// R0 接続先サーバ名
OnSNTPBegin
{
	'\0\s[6]時計を合わせますね。\w5\n\i[10]\w1\s[0]\i[31]マスカレード、\w6今何時？\w6\1\s[11]やれやれ。\w9\e'
	'\0\s[0]\i[36]時計ですね。\w5\i[10]\w2\s[1]\n\nマスカレード。\w6\1\s[11]えーとー。\w9\e'
}

// サーバに接続が確立された際
// R0 接続先サーバ名　R1 カンマでセパレートされたサーバ側の現時刻
OnSNTPCompare
{
	"\1\s[11]/
	\_qサーバー、 %(R[1][0])/%(R[1][1])/%(R[1][2]) %(R[1][3]):%(R[1][4]):%(R[1][5])\w2。\n/
	ローカル、 %(R[2][0])/%(R[2][1])/%(R[2][2]) %(R[2][3]):%(R[2][4]):%(R[2][5])\w2。\_q\n/
	\n"
	--
	if R[3] == 0
	{
		'\1\s[12]ぴったんこ。\w6\0\s[5]\i[34]おめでとうございますっ。\e'
	}
	else
	{
		"\1\s[13]その差、\w6%(R[3])秒。\w6\0\s[1]修正しますか？\n\n/
		\q0[OnSNTPExecute][修正する]/
		\q1[OnSNTPCancel][放置する]\e"
	}
}

OnSNTPExecute
{
	'\1\s[10]じゃ修正。\6\w6\s[11]\nデジタルは楽だねえ。\w6\0\s[6]\i[31]ぴったりっ。\e'
}

OnSNTPCancel
{
	'\1\s[14]お、\w6俺が時間を調べた意義は……。\w9\0\s[8]\i[16]い、いいじゃないっ。\e'
}



// ローカル時刻をサーバ時刻に修正した際
// R0 接続先サーバ名　R1 カンマでセパレートされたサーバ側の現時刻
// R2 カンマでセパレートされたローカル側の現時刻
//OnSNTPCorrect{}

// 時計合わせに失敗した際
// R0 接続先サーバ名　R1 カンマでセパレートされたサーバ側の現時刻
// R2 カンマでセパレートされたローカル側の現時刻　R3 サーバとローカルの時刻のずれ(second)
OnSNTPFailure
{
	'\1\s[13]オーケイ、\w9\s[11]正確な時間わかんない。\w6\0\s[3]……\w6な、\w6\i[10]何がオーケイなのよう。\e'
	'\1\s[13]正確な時間、\w6わかんないにゃん。\w6\0\s[0]\i[60]……。\w6\1\s[14]\n\nにゃん。\e'
}

























// # ----------------------------------------------------------------
// # ネットワーク更新


// ネットワークアップデート開始が指示
// R0 成功・失敗理由
/*
	none 更新するファイルが無かった
	changed 更新された
	timeout タイムアウトした
	md5 miss MD5が一致しなかった
	404等 そのステータスコードでの失敗
*/
OnUpdateBegin
{
	if ! ISVAR('MD5CompareFailedCount'){TmpGVList,='MD5CompareFailedCount'}
	MD5CompareFailedCount = 0

	'\0\s[30]\i[31]では、\w6明日の私を占ってみます。\i[10]\w6\1\s[11]当たるも八卦、\w6当たらぬも八卦。\e'
	'\0\s[1]それでは、\w6明日の私を占ってみましょう。\w6\1\s[10]む。\e'
	'\0\s[6]\i[36]明日の私は、\w6どうなっているでしょう。\w6\1\s[11]ふむ。\e'
	'\0\s[1]では、\w6水晶球で明日を占います。\w6\s[32]\n水晶球に集中して下さいね。\w6\1\s[12]むねきゅん。\e'
}



// 更新ファイルが確認された
OnUpdateReady
{
	_algupdatedfile = R[0] + 1

	"\0\s[32]\i[32]えと、\w6%(_algupdatedfile)の幽かな光が見えます。\w6\i[10]\w1\s[0]\i[31]\n明日の私をつくるべき、\w6新しい要素ですね。\w9\i[10]\e"
	"\0\s[32]……\w6%(_algupdatedfile)の、幽かな光が見えます。\w6\1\s[11]む。\w6\0\i[10]\w1\s[0]\i[31]\n\nどんな光なのか、\w6調べてみましょう。\w9\e"
	"\0\s[30]……\w6%(_algupdatedfile)の、\w6幽かな光が感じられます。\w6\1\s[12]お？\w6\0\i[10]\w1\s[31]\n\n見てみることにしますね。\w9\s[1]\e"
}


// 成功・完了した際
// R0 成功・失敗理由　R1 更新されたファイルのリスト（カンマ区切り）
OnUpdateComplete
{
	// 最初に保存したデータを読み込む など
	// 一時的にひとつのファイルにまとめてもいいが今はこの仕様
	OnGhostLoad2
	GetBasewareData

	if R[0] == 'none'
	{
		// 更新すべきファイルが無かった
		'\0\s[6]……\w6明日の私は、\w6今とあまり変わらない私のようです。\e'
		'\0\s[8]今の私と明日の私に、\w6目立った違いはないようです。\w6\1\s[11]そうか。\e'
	}
	else
	{
		if MD5CompareFailedCount > 0
		{
			"\0\s[0]……\w6と。\w9\i[31]\nひととおり終わりました……。\w9\1\s[11]むう。\w6\nMD5チェック失敗を%(MD5CompareFailedCount)発見。\w9\0\i[10]\i[34]\i[60]\n\nき、\w9気にしないで下さいねっ。\w6\1\s[14]\n\n待て。\e"
		}
		else
		{

			// 実際に更新が完了した
			// リロードした状態にするべきなのでOnGhostLoadなどを呼ぶ必要がある
			'\0\s[0]……\w6\i[10]\w1\s[1]終わりました。\w8\s[5]\n\nちょっと変わりましたか？\w9\1\s[11]……\w6ま、\w6変わったんだろうな。\w9\n何か。'
			'\0\s[6]……\w6\w5\i[10]\w1\s[8]どうですか？\w9\1\s[11]いや、\w6どうと言われても。'
			'\0\s[1]……\w9ということですっ。\w6\1\s[11]ふむ。'
			'\0\s[0]\i[31]終わりっ。\w6\w4\i[10]\w1\s[5]\n\n次お会いする時には、\w6\nきっと新しい私になっているはずです。\w6\1\s[13]といっても、\w6見た目には変わらないだろうけどな。'
			'\0\s[0]……\w6と。\w6\i[10]\w1\s[5]\n\n明日の私は、\w6\nこういった感じで新しくなっているかもですっ。\w6\1\s[10]……\w6む。'
			--
			'\e'
		}
	}
}




// 失敗した際
// R0 成功・失敗理由　R1 更新されたファイルのリスト（カンマ区切り）
// R3[C]ゴースト更新の際は"ghost"本体更新の際は"baseware"
OnUpdateFailure
{
	if R[0] == 'timeout'
	{
		// タイムアウトした
		'\0\s[3]……\w6なんだか、\w6いつまでたっても見えません。\w8\i[10]\w1\s[6]\n\nまた後で占えば出るかもしれないです。\w6\1\s[11]だそうです。\e'
	}
	elseif R[0] == 'md5 miss'
	{
		// MD5値が一致しなかった
		'\0\s[8]MD5値が合わないです……。\w6\1\s[11]？\e'
	}
	else
	{
		// 上記以外の理由で失敗した
		'\0\s[3]……\w6なぜか、\w6見えません。\w5\i[10]\w1\s[4]\n\n実力不足……\w6かな？\w6\1\s[13]……\w6そうか？\e'
	}
}



// ファイルのダウンロード開始
// R0 DLファイル名　R1 更新総数の現在のファイル数　R2 更新総数
OnUpdate.OnDownloadBegin //ダウンロード開始
{
	"\0\_q%(CHR(39))%(R[0])%(CHR(39))\n\n\![*] DOWNLOAD\_q\e"
}
// MD5の照合開始
// R0 比較するファイル名
OnUpdate.OnMD5CompareBegin //MD5値照合開始
{
	"\0\_q%(CHR(39))%(R[0])%(CHR(39))\n\n\![*] DOWNLOAD\n\n\![*] MD5 COMPARISON\_q\e"
}
// MD5が一致した際
// R0 比較するファイル名　R1 正しいMD5値
OnUpdate.OnMD5CompareComplete //MD5値一致
{
	"\0\_q%(CHR(39))%(R[0])%(CHR(39))\n\n\![*] DOWNLOAD\n\n\![*] MD5 COMPARISON\n　　%(R[2])\n\n\![*] %(CHR(39))%(R[0])%(CHR(39)) INSTALLED.\_q\e"
}
// MD5が一致しなかった際
// R0 比較するファイル名　R1 正しいMD5値　R2 落としたファイルのMD5値
OnUpdate.OnMD5CompareFailure //MD5値不一致
{
	MD5CompareFailedCount+=1
	"\0\_q%(CHR(39))%(R[0])%(CHR(39))\n\n\![*] DOWNLOAD\n\n\![*] MD5 COMPARISON\n　　!! FAILED !!\_q\w3\c\w3\_q%(CHR(39))%(R[0])%(CHR(39))\n\n\![*] DOWNLOAD\n\n\![*] MD5 COMPARISON\n　　!! FAILED !!\_q\w3\c\w3\_q%(CHR(39))%(R[0])%(CHR(39))\n\n\![*] DOWNLOAD\n\n\![*] MD5 COMPARISON\n　　!! FAILED !!\_q\w6\e"
}




