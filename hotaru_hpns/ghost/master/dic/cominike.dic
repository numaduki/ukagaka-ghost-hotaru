/*
	コミュニケート
*/


StartCommunicate
{
	// コミュニケートするゴーストを選ぶ
	if ! MaterializedGhostList
	{
		// 複数回ループする可能性があるが気になる程ではない
		OnTalk;return
	}
	rR[0] = ANY(MaterializedGhostList)
	"\1\s[11]む、\w6%(rR[0])……。\w6\w6\s[12]\n\n実際見るとやっぱリアルだなあ。\w6\0\s[8]……\w6\w3\i[10]\w3\s[5]%(rR[0])さん、\w6どうも。\e"
	"\1\s[11]お、\w6%(rR[0])だ。\w6\w6\s[13]\n\n……。\w6\w6\w6\0\s[8]\i[16]め、\w6目を光らせないのっ。\w6\w6\i[10]\w1\s[5]\n\n%(rR[0])さん、\w6どうも……。\e"
}

// R0 Boxからは"user"ゴーストからはゴースト名　R1 スクリプト　R* 拡張情報
OnCommunicate
{
	if R[2] == 'ECHO/1.0' // 台本コミニケ（受信）
	{
		if '\![vanishbymyself]' _in_ R[3]{return}

		rR[0] = R[0]
		R[3]
	}
	if TOLOWER(R[0])=='user'
	{ReplyToUser(R)}
	else
	{ReplyToGhost(R)}
}


ReplyToUser
{
	_str1 = ''
	_str2 = ''
	_str3 = ''

	if BasewareData[0] == 'SSP'
	{
		_str1 = '\0\![open,inputbox,talkwithuser,-1,]'
		_str3 = '\e'
	}
	else
	{
		_str3 = '\![open,inputbox,talkwithuser,-1,終了する場合は、何も入力しないでenter]\e'
	}


	if SearchWordInArray(_argv[0],'おはよう','お早う')
	{
		_t = GETTIME[4]
		if BETWEEN(_t,5,9)
		{
			_str2 = '\0\s[0]\i[31]……\w5\i[10]\w1\s[5]\i[31]おはようございますっ。\e'
		}
		else
		{
			_str2 = '\0\s[0]\i[34]\i[60]えーと……\w6\i[10]\w1\s[5]お、\w6おはようございますっ。\w9\1\s[11]……\w6ギョーカイの方ですか？\e'
		}
	}

	elseif SearchWordInArray(_argv[0],'好きだ','すきだ','好きです','すきです','愛して','あいして')
	{
		if SearchWordInArray(_argv[0],'マスカレード')
		{
			_str2 = ANY('\0\s[0]\1\s[10]ほほう。\w6\s[11]\n\n好きだと言ってくれるのは嬉しいが、\w6\w6\s[12]\n\nあいにく俺の許容範囲は\n幼稚園から小学生低学年と決まっている。\w9\0\s[0]……。\w6\i[10]\w2\s[4]\n\n……\w6ひ、\w6低すぎるよっ。\w9\1\s[11]\n\n……\w6まあ、\w6好いてくれるならある程度までは。','\0\s[0]\1\s[11]むう。\w9\s[12]\n\nしかしあいにく俺は\n貧乳のおんなのこが好きなものでね……。\w9\0\s[4]……。\w9\1\s[11]\n\nいや、\w6\w6\s[10]\nでもまあ本当に大切なのは\nナイチチじゃなくて気持ちだよね！\w9\0\s[6]\n\n……\w6こないだは『大きい方が絶対いい』\w3\nって言ってたくせに。\w6\1\s[11]\n\nえ。\w9\0\s[9]\n\n『ほたるは努力しないからいつまでたっても大きくならない。\w3\nはやく自覚しろ』\w3って\1\s[14]\n\nあああああああやめてッ言わないでッ！\w2\s[13]\n俺の紳士なイメージがー！')
		}
		elseif SearchWordInArray(_argv[0],'君','きみ','あなた','貴方','貴女','お前','おまえ')
		{
			_str2 = '\1\s[11]\0\s[5]\i[34]ど、\w6どうもありがとうございますっ。\w9\s[1]\n\n私も実は、\w6前からずっと、\w9\nみんなから好かれる占い師になりたいなー、\w6\nって……。\w9\w6\1\s[11]むう。\w9\n\n違う意味で言ってくれたんなら、\w6\n脳内のデフォルトが違うんだと思って\nどうか勘弁してやってくれ。\w6\0\s[0]\n\n？\w6\w6\w6\c\i[31]とにかく、\w6ありがとうございます。\w9\i[10]\w2\s[1]\nこれからもがんばりますね。\w6\w6\s[5]\n\nマスカレード、\w6\n私ほめられちゃったよ？\w6\1\s[11]\n\nよかったねえ。'
		}
		elseif SearchWordInArray(_argv[0],'ほたる','ホタル','蛍')
		{
			_str2 = '\1\s[10]\0\s[5]あ。\w9\i[10]\w2\s[0]\i[31]\n私も好きですよ。\w6ほたる。\w6\1\s[11]？\w6\w6\0\i[10]\w2\s[6]\n\nでも、\w6実際に光りながら飛んでいるのは見たことなくて……。\w6\w6\1\s[12]\n\nほたるさんほたるさん。\w9\0\i[10]\w2\s[0]\i[31]\i[71]\n\nもしどこかで見れるとか噂を聞いたら、\w6私にも教えてくださいねっ。\w9\n\n私も1度は見てみたいです。\w6\1\s[13]\n\nほたるさーん。'
		}
		else
		{
			_str2 = "\1\s[10]\0\s[0]\i[31]何にしても、\w6\n誰かに好きだと思われているというのは。\w6\w6\i[10]\w2\s[5]\n\nとても素敵なことですよねっ。\w9\1\s[11]そうだなあ……。\w6\s[12]\n俺も%(Character)が好きだ。\w6\w6\i[0]\n\n永遠にしたい。\w9\0\s[4]\n\nえ、\w6永遠？"
		}
	}

	elseif SearchWordInArray(_argv[0],'寒い','さむい','寒く','さむく')
	{
		_str2 = '\0\s[4]寒いと、\w6外で夜を越すのは大変なんです。\w9\i[10]\w2\s[8]\n\nここは冬でもあったかいですし、\w6\n安心して眠れますし。\w9\n本当に助かってますっ。\w9\1\s[10]む。\w9\s[11]\nぬくぬくとアニメを観つつ\n萌えれるというわけだ。\w6\w9\0\s[0]\n\n……。\w6\w9\s[7]\n……。\w6\1\s[13]\n\n何故に。'
	}

	elseif SearchWordInArray(_argv[0],'タマゴサンド','卵サンド')
	{
		if SearchWordInArray(_argv[0],'好き','すき','好物','嫌い','きらい','嫌','いや')
		{
			_str2 = ANY("\0\s[8]\1\s[11]……\w6好きというか、\w9嫌いというか、\w9\s[13]\nぶっちゃけもうイヤというか。\w9\0\s[9]……\w6い、\w6いやって言わないのっ。\w9\1\s[11]\n\n好意で安く譲ってもらえてる以上は\n選り好みもできない。\w9\0\s[3]\n\nうーん……。\w6\w6\1\s[10]\n\nつまりアレだ。\w6\s[13]\n『%(OtakuHobby)に命かけてます』\w6\nとか言ってるけど\s[14]本当はそれしかやることがない\w3っていうオタクと一緒なんだよね。\w6\w6\s[11]\nこれは。\w9\0\s[8]\n\n……\w6オタク？",'\s[8]す、\w6好き嫌いというか……、\w6\w6\i[10]\w2\s[6]\i[60]\i[71]\n食べて、\w6生きてますっ。\w6\1\s[11]……。\w9\n\n……\w6まあ、\w6生きる手段だな。\w6\0\n\n……\w6そういうこと……\w9かな。')
		}
		else
		{
			_str2 = '\0\s[0]\1\s[14]タマゴサンドって言うな。\w9\s[11]\n\n泣くよ。\w9\0\s[8]……\w6よ、\w6喜んで食べさせて頂いてますっ。\e'
		}
	}

	elseif SearchWordInArray(_argv[0],'占い','うらない')
	{
		if SearchWordInArray(_argv[0],'好き','すき')
		{
			_str2 = '\0\s[1]占いが好きか、\w6ですか？\w9\i[10]\w2\s[0]\i[31]\n好きですよっ。\w9\1\s[11]ていうか嫌いでもこれで金を稼がないと\s[14]\n野垂れ死ぬからな。\w9\0\i[10]\w2\s[4]\n\nそ、\w6それは……。'
		}
		elseif SearchWordInArray(_argv[0],'当たる','あたる','的中する','当たり','あたり','当たら','あたら','的中しな')
		{
			_str2 = '\0\s[8]占いが当たるか、\w6ですか？\w9\i[10]\w2\s[4]\nさ、\w6最善の努力がしてます……\w9けどっ。\w9\1\s[14]正直な話、\w6あんまり外れて困るようなことを占えといわれてもなあ。\w6\w6\s[11]\n\n他人の幸せの形までは面倒見きれないぞ。\w6\0\i[10]\w2\s[3]\n\nうーん……。'
		}
		elseif SearchWordInArray(_argv[0],'胡散臭い','うさんくさい','胡散くさい','いんちき','インチキ')
		{
			_str2 = '\0\s[4]えーと……。\w6\w6\1\s[11]……\w6まあ、\w6\nインチキとか胡散臭いとか言われてしまえば\nそれまでなんだが。\w6\w6\0\s[0]\i[31]\n\n……\w6悩んでいるときのアドバイス程度に。\w6\w6\i[10]\w2\s[5]\n利用していただければ。\w9\1\n\nふむ。'
		}
		else
		{
			_str2 = "\0\s[5]あ、\w6占いですか？\w9\i[10]\w2\s[0]\i[36]\n\n%(username)さんも趣味で占いとか、\w6\nしないんですか？\w6\1\s[11]朝のテレビで見るくらいか？"
		}
	}
	elseif SearchWordInArray(_argv[0],'?','？')
	{
		_str2 = ANY("\0\s[0]%(_argv[0])\w6\w6\n\n\i[10]\w2\s[6]\i[31]\i[21]……\w6もちろんですよっ。\w9\1\s[11]……\w6む。","\0\s[0]%(_argv[0])\w6\w6\n\n\i[10]\w2\s[6]\i[21]\i[60]……\w6と、\w6時と場合によりますけど……。\w9\1\s[11]……。")
	}
	//## ひっかからない。汎用トーク
	else
	{
		//## ひっかからない。汎用トーク
		//_str2 = ANY("\1\s[11]そうか。\w9\s[12]\n\n%(username2)もやはり\n『%(AnimationFilm)』好きか。\w6\0\s[4]き、\w6決めつけないのっ。","\1\s[11]そうか。\w6\n『%(_argv[0])』か。\w6\w6\s[12]\n\n……\w6俺も若い頃はそんな感じだったなあ。\0\s[0]？")
		_str2 = "\0\s[0]\1\s[11]『\_q%(_argv[0])\_q』――。\w6\w6\n\n\s[16]それはむしろ、\w6「%(RadioProgram)」\w3に送るべきだな。"
	}
	"%(_str1)%(_str2)%(_str3)"
}


SearchWordInArray
{
	for _i=1;_i<ARRAYSIZE(_argv);_i++
	{
		if _argv[_i] _in_ _argv[0]
		{1;return}
	}
	0
}







ReplyToGhost
{
	// 送ってきたゴーストにスクリプトを送るならrR[0]にゴースト名を入れる
	// 送らないなら特に何もしなくていい
	if 'こんにちは' _in_ _argv[1]
	{
		'\0\s[5]こんにちは。\e'
	}
	elseif '元気' _in_ _argv[1]
	{
		'\0\s[0]\i[31]……\w6元気ですよっ。\w6\n\i[10]\w2\s[1]……\w6なんとか……。\w6\1\s[11]……\w6むう。\w9\0\s[5]\i[36]\n\nありがとうございます。\e'
	}
	else
	{
		'\0\s[0]\i[31]……。\e'
		return
	}
	rR[0]=_argv[0]
}

